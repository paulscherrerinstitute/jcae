apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
    }
}
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'


sourceCompatibility = 1.7

version = '2.9.7'
group = 'ch.psi'

repositories {  
    mavenCentral()
    maven { url "http://artifacts.psi.ch/artifactory/libs-releases" }
    maven { url "http://artifacts.psi.ch/artifactory/libs-snapshots" }
}

dependencies {
    compile 'org.epics:caj:1.1.12'
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

uploadArchives {  
    repositories {
        mavenDeployer {
            repository(url: artifactoryUrlLibSnap){
            	authentication(userName: artifactoryUser, password: artifactoryPwd)
            }
            pom.groupId = group
            pom.artifactId = project.name
        }          
    }
}


task fatJar(type: Jar) {
    baseName = project.name + '_all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}


publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            groupId group
            artifactId project.name
            version version
        }
    }
}
    
bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']     
    pkg {
        repo = 'hltools'
        name = project.name
        userOrg = 'paulscherrerinstitute'
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/paulscherrerinstitute/jcae'
        version {
            name = project.version
        }         
    }
}



