plugins {
    id "java-library"
    id "eclipse"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.4"
}

sourceCompatibility = 1.7

version = '2.9.8'
group = 'ch.psi'

repositories {  
    mavenCentral()
    maven { url "https://artifacts.psi.ch/artifactory/libs-releases" }
    maven { url "https://artifacts.psi.ch/artifactory/libs-snapshots" }
}

dependencies {
    api 'org.epics:caj:1.1.12'
    testImplementation group: 'junit', name: 'junit', version: '4.+'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}


task fatJar(type: Jar) {
    archiveBaseName = project.name + '_all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}


publishing {
    repositories {
        maven {
            credentials {
                username artifactoryUser 
                password artifactoryPwd
            }
            url = artifactoryUrlLibSnap
        }
    }

    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId group
            artifactId project.name
            version version
        }
    }
}
    
bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication'] 
    override = true
    publish = true
    pkg {
        repo = 'hltools'
        name = project.name
        userOrg = 'paulscherrerinstitute'
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/paulscherrerinstitute/jcae'
        version {
            name = project.version
        }         
    }
}



