plugins {
    id "java-library"
    id "eclipse"
    id "maven-publish"
}

sourceCompatibility = 1.7

version = '3.0.0'
group = 'ch.psi'

repositories {  
    mavenCentral()
}

dependencies {
    api 'org.epics:caj:1.1.15'
    testImplementation group: 'junit', name: 'junit', version: '4.+'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task fatJar(type: Jar) {
    archiveBaseName = project.name + '_all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}


publishing {
    repositories {
        maven {
            credentials {
                username githubUser 
                password githubPwd
            }
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/paulscherrerinstitute/jcae"
        }        
        maven {
            credentials {
                username artifactoryUser 
                password artifactoryPwd
            }
            name = "Artifactory"
            url = artifactoryUrlLibSnap
        }
        
    }

    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId group
            artifactId project.name
            version version
        }
    }
}        